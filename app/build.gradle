apply plugin: 'com.android.application'

android {
    compileSdkVersion 27
    defaultConfig {
        applicationId "me.bakumon.ugank"
        minSdkVersion 15
        targetSdkVersion 27
        versionCode 10
        versionName "mvp-2.2.2"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true
    }
    dataBinding {
        enabled = true
    }
    signingConfigs {
        release {
            storeFile file("ugank.jks")
            storePassword KEYSTORE_PASSWORD
            keyAlias KEY_ALIAS
            keyPassword KEY_PASSWORD
        }
    }
    buildTypes {
        debug {
            // 签名
            signingConfig signingConfigs.release
        }
        release {
            //  混淆
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            // Zipalign优化
            zipAlignEnabled true
            // 移除无用的resource文件
            shrinkResources true
            // 签名
            signingConfig signingConfigs.release
        }
        // gradle 插件 3.0.0 以后版本
        android.applicationVariants.all { variant ->
            variant.outputs.all {
                outputFileName = "Ugank-${variant.baseName}-${variant.versionCode}-V${variant.versionName}.apk"
            }
        }
    }
}
dependencies {
    // jar
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    // 测试
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.1'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.1'
    // 支持库
    implementation 'com.android.support:appcompat-v7:27.0.2'
    implementation 'com.android.support:support-v4:27.0.2'
    implementation 'com.android.support:design:27.0.2'
    // ViewModel and LiveData
    implementation "android.arch.lifecycle:extensions:1.0.0"
    annotationProcessor "android.arch.lifecycle:compiler:1.0.0"
    // 第三方库
    implementation 'com.github.CymChad:BaseRecyclerViewAdapterHelper:2.9.34'
    implementation 'com.github.Andy671:Dachshund-Tab-Layout:v0.3.0'
    implementation 'io.reactivex:rxandroid:1.2.1'
    implementation 'com.squareup.retrofit2:adapter-rxjava:2.1.0'
    implementation 'com.squareup.retrofit2:retrofit:2.3.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.3.0'
    // 使用 picasso 后获取图片的 palette(调色板)
    implementation 'com.github.florent37:picassopalette:2.0.0'
    implementation 'com.squareup.picasso:picasso:2.5.2'
    // material 风格的 dialogs
    implementation 'com.afollestad.material-dialogs:commons:0.9.3.0'
    // 滑动返回
    implementation 'cn.bingoogolapple:bga-swipebacklayout:1.1.8@aar'
    // 数据库
    implementation 'org.litepal.android:core:1.5.0'
    // 美化 Toast
    implementation 'com.github.GrenderG:Toasty:1.1.3'
    // 下落 emoji
    implementation 'com.luolc:emoji-rain:0.1.1'
    // flex 布局
    implementation 'com.google.android:flexbox:0.3.0'
    // 手势查看大图
    implementation 'com.bm.photoview:library:1.4.1'
    // 状态布局管理
    implementation 'com.github.Bakumon:StatusLayoutManager:1.0.1'
    // LeakCanary
    testCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.5.1'
    debugCompile 'com.squareup.leakcanary:leakcanary-android:1.5.1'
    releaseCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.5.1'

    // 统一支持库使用 27.0.2 版本
    configurations.all {
        resolutionStrategy.eachDependency { DependencyResolveDetails details ->
            def requested = details.requested
            if (requested.group == 'com.android.support') {
                if (!requested.name.startsWith("multidex")) {
                    details.useVersion '27.0.2'
                }
            }

        }
    }
}
